import groovy.json.JsonSlurper

pluginManagement {
	repositories {
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven {
			name = 'Jitpack'
			url = "https://jitpack.io"
		}
		resolutionStrategy {
			eachPlugin {
				switch (requested.id.id) {
					case "com.replaymod.preprocess": {
						useModule("com.github.ReplayMod:preprocessor:${requested.version}")
						break
					}
				}
			}
		}
		mavenCentral()
		gradlePluginPortal()
	}
}

def settings = new JsonSlurper().parseText(file('settings.json').text)
for (String version : settings.get("versions")) {
	include(":$version")
	def proj = project(":$version")
	proj.projectDir = file("versions/$version")
	proj.buildFileName = "../../common.gradle"
}